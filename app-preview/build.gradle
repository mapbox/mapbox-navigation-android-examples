apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.mapbox.maps.token'
apply from: "${rootDir}/gradle/script-git-version.gradle"
apply from: "${rootDir}/gradle/ktlint.gradle"

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.mapbox.navigation.examples.preview"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode gitNumberOfCommits
        versionName gitTagDescription
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            minifyEnabled true
            manifestPlaceholders = [enableCrashReporting: "true"]
        }
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            def srcDirs = file("$projectDir/src/main/java/com/mapbox/navigation/examples/preview")
                    .listFiles({ it.isDirectory() } as FileFilter)
                    .collect { it.name }

            res.srcDirs = ['src/main/res']
            srcDirs.forEach {
                res.srcDirs += 'src/main/java/com/mapbox/navigation/examples/preview/' + it + '/res'
            }
        }
    }
}

dependencies {
    // Mapbox Navigation SDK
    implementation "com.mapbox.navigationcore:navigation:3.0.0-rc.5"
    implementation "com.mapbox.navigationcore:ui-maps:3.0.0-rc.5"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.21"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "com.google.android.material:material:1.7.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.gms:play-services-location:20.0.0"
    implementation "com.squareup.leakcanary:leakcanary-android:2.9.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

}
