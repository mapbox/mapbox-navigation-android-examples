apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.mapbox.maps.token'
apply from: "${rootDir}/gradle/script-git-version.gradle"
apply from: "${rootDir}/gradle/ktlint.gradle"

android {
    namespace "com.mapbox.navigation.examples"
    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "com.mapbox.navigation.examples"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode gitNumberOfCommits
        versionName gitTagDescription
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            minifyEnabled true
            manifestPlaceholders = [enableCrashReporting: "true"]
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/java/com/mapbox/navigation/examples/dropinui/res',
                    'src/main/java/com/mapbox/navigation/examples/standalone/res',
            ]

            def standaloneDirs = file("$projectDir/src/main/java/com/mapbox/navigation/examples/standalone")
                    .listFiles({ it.isDirectory() } as FileFilter)
                    .collect { it.name }
            standaloneDirs.forEach {
                res.srcDirs += 'src/main/java/com/mapbox/navigation/examples/standalone/' + it + '/res'
            }

            def dropInUIDirs = file("$projectDir/src/main/java/com/mapbox/navigation/examples/dropinui")
                    .listFiles({ it.isDirectory() } as FileFilter)
                    .collect { it.name }
            dropInUIDirs.forEach {
                res.srcDirs += 'src/main/java/com/mapbox/navigation/examples/dropinui/' + it + '/res'
            }
        }
    }
}

dependencies {
    // Mapbox Navigation SDK
    implementation "com.mapbox.navigationcore:navigation:3.11.6"
    implementation "com.mapbox.navigationcore:ui-components:3.11.6"
    implementation "com.mapbox.extension:maps-compose:11.14.4"
    
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.21"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "com.google.android.material:material:1.7.0"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.gms:play-services-location:20.0.0"
    implementation "com.squareup.leakcanary:leakcanary-android:2.9.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    implementation "androidx.activity:activity-compose:1.9.2"

    // Compose BOM
    implementation platform('androidx.compose:compose-bom:2024.06.00')
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

}
